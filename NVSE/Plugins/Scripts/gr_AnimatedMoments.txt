; Animated Moments - Stentorious + Hitman

; Check for requirements
if eval GetNVSEVersion < 6 || (GetNVSEVersion == 6 && GetNVSERevision < 4)
	MessageBoxEx "Animated Moments missing requirement!%rInstall xNVSE 6.4.0+."
	return
endif
if GetPluginVersion "JohnnyGuitarNVSE" < 390
	MessageBoxEx "Animated Moments missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
	return
endif
if GetPluginVersion "ShowOffNVSE Plugin" < 181
	MessageBoxEx "Animated Moments missing requirement!%rInstall latest ShowOff NVSE plugin."
	return
endif
if GetPluginVersion "kNVSE" < 37
	MessageBoxEx "Animated Moments missing requirement!%rInstall latest kNVSE plugin."
	return
endif
if GetPluginVersion "lStewieAl's Tweaks" < 906
	MessageBoxEx "Animated Moments missing requirement!%rInstall latest lStewieAl's Tweaks."
	return
endif

; Load anim triggers
int iType
int iSize
int iIndex
int iHideHUD
int iDisableControlFlags
float fAnimDelay
float fCameraShakeStr
float fCameraShakeDur
ref rFirst
ref rSecond
array_var aJSON
array_var aEntry
array_var aForE1
array_var aForE2
string_var sJSONPath
string_var sAnimPath
string_var sType
string_var sAux

array_var aFiles = GetFiles "Meshes\AnimatedMoments" "*.json"

if eval (iSize = ar_Size aFiles) > 0
	while (iSize -= 1) > -1
		sJSONPath = sv_Construct "Data\Meshes\AnimatedMoments\%z" (aFiles[iSize])

		; Check valid JSON
		if eval TestExpr (aJSON = ReadFromJSON (sJSONPath))
		else
			PrintC "Animated Moments: Invalid -> %z" sJSONPath
			continue
		endif

		; Load anim entries
		foreach aForE1 <- aJSON

			aEntry = *aForE1

			; Had valid animation path
			if eval ar_HasKey aEntry "animPath" == 0
				continue
			endif

			; Trigger type
			if eval ar_HasKey aEntry "imod"
				iType = 1
				sType = "imod"
			elseif eval ar_HasKey aEntry "questStage"
				iType = 2
				sType = "questStage"
			elseif eval ar_HasKey aEntry "questObjComplete"
				iType = 3
				sType = "questObjComplete"
			elseif eval ar_HasKey aEntry "questObjDisplay"
				iType = 4
				sType = "questObjDisplay"
			elseif eval ar_HasKey aEntry "packageStart"
				iType = 5
				sType = "packageStart"
			elseif eval ar_HasKey aEntry "enable"
				iType = 6
				sType = "enable"
			elseif eval ar_HasKey aEntry "disable"
				iType = 7
				sType = "disable"
			else
				continue
			endif

			; Trigger has entries
			if eval (ar_Size (aEntry[(sType)])) < 1
				continue
			endif

			; Load anim parameters
			sAnimPath = aEntry["animPath"]

			; Anim delay
			fAnimDelay = 0
			if eval ar_HasKey aEntry "animDelay"
				fAnimDelay = aEntry["animDelay"]
			endif

			; Toggle HUD
			iHideHUD = 0
			if eval ar_HasKey aEntry "hideHUD"
				iHideHUD = aEntry["hideHUD"]
			endif

			; Player control flags
			iDisableControlFlags = 0
			if eval ar_HasKey aEntry "disableControlFlags"
				iDisableControlFlags = aEntry["disableControlFlags"]
			endif

			; Camera shake
			fCameraShakeStr = 0
			fCameraShakeDur = 0
			if eval ar_HasKey aEntry "cameraShake" && ar_HasKey aEntry["cameraShake"] "strength" && ar_HasKey aEntry["cameraShake"] "duration"
				fCameraShakeStr = aEntry["cameraShake"]["strength"]
				fCameraShakeDur = aEntry["cameraShake"]["duration"]
			endif

			foreach aForE2 <- aEntry[(sType)]

				; Validate form
				if eval iType == 1 || iType == 6 || iType == 7
					rFirst = EditorIDToFormID (*aForE2)
					sAux = sv_Construct "*AnimMom_%g" iType
				elseif eval iType == 5
					rFirst = EditorIDToFormID (aForE2["value"]["actor"])
					rSecond = EditorIDToFormID (aForE2["value"]["package"])
					if eval IsFormValid rSecond == 0
						continue
					endif
					sAux = sv_Construct "*AnimMom_%g_%z" iType (aForE2["value"]["package"])
				else
					rFirst = EditorIDToFormID (aForE2["value"]["quest"])
					iIndex = aForE2["value"]["index"]
					sAux = sv_Construct "*AnimMom_%g_%g" iType iIndex
				endif
				if eval IsFormValid rFirst == 0 || AuxVarSize (sAux) rFirst > 0
					continue
				endif

				; Set event handlers
				if eval iType == 1
					SetEventHandler "OnApplyIMOD" (CompileScript "AnimatedMoments\OnApplyIMOD.gek") "first"::rFirst
				elseif eval iType == 2
					SetOnQuestStageEventHandler (CompileScript "AnimatedMoments\OnQuestStage.gek") 1 rFirst iIndex
				elseif eval iType == 3
					SetEventHandler "ShowOff:OnCompleteObjective" (CompileScript "AnimatedMoments\OnCompleteObjective.gek") "first"::rFirst "second"::iIndex
				elseif eval iType == 4
					SetEventHandler "ShowOff:OnDisplayObjective" (CompileScript "AnimatedMoments\OnDisplayObjective.gek") "first"::rFirst "second"::iIndex
				elseif eval iType == 5
					SetEventHandler "OnPackageStart" (CompileScript "AnimatedMoments\OnPackageStart.gek") "first"::rFirst "second"::rSecond
				elseif eval iType == 6
					SetEventHandler "OnEnable" (CompileScript "AnimatedMoments\OnEnable.gek") "first"::rFirst
				elseif eval iType == 7
					SetEventHandler "OnDisable" (CompileScript "AnimatedMoments\OnDisable.gek") "first"::rFirst
				endif

				; Set Aux
				AuxVarSetStr (sAux) (sAnimPath) 0 rFirst
				AuxVarSetFlt (sAux) fAnimDelay 1 rFirst
				AuxVarSetFlt (sAux) iHideHUD 2 rFirst
				AuxVarSetFlt (sAux) iDisableControlFlags 3 rFirst
				AuxVarSetFlt (sAux) fCameraShakeStr 4 rFirst
				AuxVarSetFlt (sAux) fCameraShakeDur 5 rFirst

			loop
		loop
	loop
endif

aFiles = aJSON = aEntry = aForE1 = aForE2 = ar_Null
sv_Destruct sJSONPath sAnimPath sType sAux
